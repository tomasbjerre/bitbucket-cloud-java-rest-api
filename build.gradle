plugins {
	id "se.bjurr.gradle.bundle-jar" version "1.3.10"
	id "org.openapi.generator" version "7.16.0"
}

javaExtension {
	javaVersion = 17
}

formatting {
	formattingExcludedPatterns = ["src/gen/java/**"]
}

dependencies {
	api 'io.swagger:swagger-annotations:1.6.16'
	api 'jakarta.ws.rs:jakarta.ws.rs-api:4.0.0'
	api "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.20.0"
	api "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.20.0"
	api 'jakarta.validation:jakarta.validation-api:3.1.1'
	api 'jakarta.annotation:jakarta.annotation-api:3.0.0'
}

openApiGenerate {
	generatorName = "jaxrs-spec"
	skipOverwrite = false
	inputSpec = "$rootDir/openapi.yml".toString()
	validateSpec = false
	skipValidateSpec = true
	outputDir = "$rootDir".toString()
	apiPackage = "se.bjurr.bitbucketcloud.gen.api"
	modelPackage = "se.bjurr.bitbucketcloud.gen.model"
	configOptions = [
		interfaceOnly: "true",
		generatePom: "false",
		useJackson: "true",
		useJakartaEe: "true"
	]
}

processTestResources.dependsOn 'openApiGenerate'
compileJava.dependsOn 'openApiGenerate'
compileGroovy.dependsOn 'openApiGenerate'

afterEvaluate {
	tasks.findByName("spotlessGroovyGradle")?.dependsOn("openApiGenerate")
	tasks.findByName("spotlessJava")?.dependsOn("openApiGenerate")
	tasks.findByName("spotlessJson")?.dependsOn("openApiGenerate")
}

tasks.named("sourcesJar") {
	dependsOn(tasks.named("openApiGenerate"))
}

tasks.named("javadocJar") {
	dependsOn(tasks.named("openApiGenerate"))
}